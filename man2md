#!/usr/bin/awk -f
# man2md generate a markdown document from a man page
# man2md is in public domain

BEGIN {
	example = 0
	bullet = 0
	indent = 0
	continueline = 0
	emphasis = ""
}

# remove the initial .SOMETHING tag
function removetag() {
	sub("^\\...? *", "")
}

# process twofold font markup (.IR, .BR, etc)
function twotags(m1, m2, tag, n, beg, i) {
	tag = $1
	if (example) {
		m1 = ""
		m2 = ""
	}
	removetag()
	beg = 1
	if (match($0, "\"")) {
		n = split($0, a, "\"")
		if (n > 0 && a[n] == "")
			n--
		if (n > 1 && a[1] == "")
			beg = 2
	} else {
		n = split($0, a)
	}
	$0 = ""
	for (i = beg; i <= n; i++) {
		if ((i - beg + 1) % 2) {
			sub("^ *", "&" m1, a[i])
			sub(" *$", m1 "&", a[i])
		} else {
			sub("^ *", "&" m2, a[i])
			sub(" *$", m2 "&", a[i])
		}
		$0 = $0 a[i]
	}
	$0 = tag " " $0
	if (!example)
		$0 = $0 " "
}

# process onefold font markup (.B, .I)
function onetag(m, tag) {
	tag = $1
	removetag()
	if (example)
		m = ""
	if (match($0, "^\"")) {
		sub("^\"", "")
		sub("\"$", "")
	}
	$0 = sprintf(m "%s" m " ", $0)
	$0 = tag " " $0
}

# process inline font change
function inline(n, c) {
	while ((n = match($0, "\\\\f.")) != 0) {        # inline font change
		c = substr($0, n + 2, 1)
		if (c == "B") {
			emphasis = "**"
		} else if (c == "I") {
			emphasis = "_"
		}
		if (example) {
			emphasis = ""
		}
		sub("\\\\f.", oldemphasis emphasis)
		oldemphasis = emphasis
		emphasis = ""
	}
	if (!example) {
		gsub("\\\\e\\*", "\\\\*")
		gsub("\\\\e\\_", "\\\\_")
	}
	gsub("\\\\e", "\\")                     # backslash
}

# get initial indentation
function getindentation(indent, i, str) {
	str = ""
	for (i = 0; i < indent; i++) {
		str = str "    "
	}
	return str
}

# remove normal comment
$1 ~ /^\.\\"$/ {
	$0 = ""
	ignore = 1
}

# do substitutions
$0 {
	# Replace troff digraphs by their symbols
	gsub("\\\\\\(co", "©")
	gsub("\\\\\\(<=", "≤")
	gsub("\\\\\\(>=", "≥")
	gsub("\\\\\\(!=", "≠")
	gsub("\\\\\\(+-", "±")
	gsub("\\\\\\(mu", "×")
	gsub("\\\\\\(di", "÷")
	gsub("\\\\\\*?\\(aq", "\'")             # single quote
	gsub("\\\\\\*?\\(dq", "\"")             # double quote
	gsub("\\\\\\*?\\(lq", "“")              # left double quote
	gsub("\\\\\\*?\\(rq", "”")              # right double quote

	for (i in ds)                           # .ds definitions
		gsub("\\\\\\*\\(" i, ds[i])
	gsub("\\\\-", "-")                      # hyphen
	gsub("\\\\( |~)", " ")                  # unbreakable space (converted to regular space)
	gsub("\\\\0", " ")                      # digit-size space (converted to regular space)
	gsub("\\\\(\\||\\^)", "")               # 1/6th and 1/12th space (ignored)
	gsub("\\\\s([+-]?[0-9]|\\([+-]?[0-9][0-9]|[+-]?\\([0-9][0-9])", "") # font size change (ignored)
	sub("\\\\\".*$", "")
	if (!example) {
		gsub("``", "“")                 # replace ``...'' with “...”
		gsub("''", "”")                 # replace ``...'' with “...”
		gsub("\\*", "\\*")
		gsub("\\_", "\\_")
	}
	if ($0 ~ /\\c$/) {
		continueline = 1
		sub("\\\\c$", "")
	}
}

# conditional markup (ignored)
$1 ~ /^\.(if|ie|el)$/ {
	if (match($0, "\\\\\\{") != 0) {
		while (match($0, "^\\.\\\\\\}") == 0) {
			getline
		}
	}
	$0 = ""
	ignore = 1
}

# ignored markup
$1 == ".ig" {
	end = $2
	while (match($0, "^\\.\\.$") == 0 && match($0, "^\\." end "$") == 0) {
		getline
	}
	$0 = ""
	ignore = 1
}

# add space after non-example lines
$1 ~ /^[^.]/ {
	if (!example)
		$0 = $0 " "
}

# normal macro definition (ignored)
$1 == ".de" {
	while (match($0, "^\\.\\.$") == 0) {
		getline
	}
	$0 = ""
	ignore = 1
}

# inline macro definition
$1 == ".ds" {
	removetag()
	macro = $1
	sub("^[^ ]* ", "")
	ds[macro] = $0
	$0 = ""
	ignore = 1
}

# page control (ignored)
$1 ~ /^\.(bp|ne|sv|os)$/ {
	$0 = ""
	ignore = 1
}

# spacing control (ignored)
$1 == ".sp" {
	$0 = ""
	ignore = 1
}

# indentation control (ignored)
$1 == ".in" {
	$0 = ""
	ignore = 1
}

# temporary indentation control (ignored)
$1 == ".ti" {
	$0 = ""
	ignore = 1
}

# adjust empty space before paragraph (ignored)
$1 == ".PD" {
	$0 = ""
	ignore = 1
	pd = 1
}

# title heading
$1 == ".TH" {
	$0 = sprintf("# %s(%s)\n", $2, $3)
}

# section heading
$1 == ".SH" {
	tag = $1
	removetag()
	$0 = sprintf("%s \n\n## %s\n\n", tag, $0)
}

# subsection heading
$1 == ".SS" {
	tag = $1
	removetag()
	$0 = sprintf("%s \n\n### %s\n\n", tag, $0)
}

# new paragraph
$1 ~ /^\.(PP|P|LP|IP|TP)$/ {
	initial = "\n"
	indentation = getindentation(indent)
	if (rs) {
		initial = ""
		indentation = ""
	}
	if ($1 == ".TP") {
		$0 = initial indentation "* "
		tp = 2
	} else if ($1 == ".IP" && $2 == "\\(bu") { 
		$0 = initial indentation "* "
	} else {
		$0 = initial "\n" indentation
	}
}

# begin indented section
$1 == ".RS" {
	rs = 2
	indent++
	indentation = getindentation(indent)
	$0 = "\n\n" indentation
}

# end indented section
$1 == ".RE" {
	$0 = ""
	indent--
}

# begin example (converted as ```)
$1 == ".EX" {
	example = 1
	$0 = "\n```"
}

# end example (converted as ```)
$1 == ".EE" {
	example = 0
	$0 = "```\n"
}

# no-fill mode
$1 == ".nf" {
	example = 1
	$0 = "\n```"
}

# fill mode
$1 == ".fi" {
	example = 0
	$0 = "```\n"
}

# line break
$1 == ".br" {
	indentation = getindentation(indent)
	$0 = "\n" indentation
}

# multi-line font changing (ignored)
$1 == ".ft" {
	$0 = ""
	ignore = 1
}

# bold font
$1 == ".B" {
	onetag("**")
}

# italic font
$1 == ".I" {
	onetag("_")
}

# roman-to-bold font
$1 == ".RB" {
	twotags("", "**")
}

# bold-to-roman font
$1 == ".BR" {
	twotags("**", "")
}

# bold-to-italic font
$1 == ".BI" {
	twotags("**", "_")
}

# italic-to-bold font
$1 == ".IB" {
	twotags("_", "**")
}

# italic-to-roman font
$1 == ".IR" {
	twotags("_", "")
}

# roman-to-italic font
$1 == ".RI" {
	twotags("", "_")
}

# process and print line
{
	removetag()
	gsub("\\\\&", "")               # remove zero-width character
	inline()
	if (!example) {
		# this is necessary for <...> don't be interpreted as an html tag
		gsub("<", "\\<")
	}
	if ($0 == "\\(bu ") {           # check if content is a bullet
		$0 = ""
		bullet = 1
	}
	printf "%s", $0
	if (!continueline && example && !ignore)
		print ""
	if (pd == 0) {
		indentation = getindentation(indent)
		if (tp == 1 && bullet == 0) {
			printf "\n\n" indentation "  "
		}
		if (tp)
			tp--
	}
	if (rs)
		rs--
	pd = 0
	bullet = 0
	ignore = 0
	continueline = 0
}
