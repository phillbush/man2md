#!/usr/bin/awk -f
# man2md generate a markdown document from a man page
# man2md is in public domain

BEGIN {
	example = 0
	emphasis = ""
}

# remove the initial .SOMETHING tag
function removetag() {
	sub("^\...? *", "")
}

# process twofold font markup (.IR, .BR, etc)
function twotags(m1, m2, n, beg, i) {
	removetag()
	beg = 1
	if (match($0, "\"")) {
		n = split($0, a, "\"")
		if (n > 0 && a[n] == "")
			n--
		if (n > 1 && a[1] == "")
			beg = 2
	} else {
		n = split($0, a)
	}
	$0 = ""
	for (i = beg; i <= n; i++) {
		if ((i - beg + 1) % 2) {
			sub("^ *", "&" m1, a[i])
			sub(" *$", m1 "&", a[i])
		} else {
			sub("^ *", "&" m2, a[i])
			sub(" *$", m2 "&", a[i])
		}
		$0 = $0 a[i]
	}
	$0 = $0 " "
}

# process inline markup
function inline(n, c) {
	gsub("\\\\\\*?\\(dq", "\"")             # double quote
	gsub("\\\\\\*?\\(lq", "â€œ")              # left double quote
	gsub("\\\\\\*?\\(rq", "â€")              # right double quote
	for (i in ds)                           # .ds definitions
		gsub("\\\\\\*\\(" i, ds[i])
	gsub("\\\\-", "-")                      # hyphen
	gsub("\\\\e", "\\")                     # backslash
	gsub("\\\\( |~)", " ")                  # unbreakable space (converted to regular space)
	gsub("\\\\0", " ")                      # digit-size space (converted to regular space)
	gsub("\\\\(\\||\\^)", "")               # 1/6th and 1/12th space (ignored)
	gsub("\\\\&", "")                       # zero-width character (ignored)
	gsub("\\\\s([+-]?[0-9]|\\([+-]?[0-9][0-9]|[+-]?\\([0-9][0-9])", "") # font size change (ignored)
	while ((n = match($0, "\\\\f.")) != 0) {        # inline font change
		c = substr($0, n + 2, 1)
		if (c == "B") {
			emphasis = "**"
		} else if (c == "I") {
			emphasis = "_"
		}
		sub("\\\\f.", emphasis)
	}
}

# conditional markup (ignored)
/^\.(if|ie|el) / {
	if (match($0, "\\\\\\{") != 0) {
		while (match($0, "^\\.\\\\\\}") == 0) {
			getline
		}
	}
	$0 = ""
}

# add space after non-example lines
/^[^.]/ {
	if (!example)
		$0 = $0 " "
}

# macro definition
/^\.ds / {
	removetag()
	n = split($0, a)
	sub("^[^ ]* ", "")
	ds[a[1]] = $0
	$0 = ""
}

# comment
/^\.\\"/ {
	$0 = ""
}

# page break (ignored)
/^\.bp ?/ {
	$0 = ""
}

# adjust empty space before paragraph (ignored)
/^\.PD/ {
	$0 = ""
	pd = 1
}

# title heading
/^\.TH / {
	split($0, a)
	$0 = sprintf("# %s(%s)\n", a[2], a[3])
}

# section heading
/^\.SH / {
	removetag()
	$0 = sprintf("\n\n## %s\n\n", $0)
}

# subsection heading
/^\.SS / {
	removetag()
	$0 = sprintf("\n\n### %s\n\n", $0)
}

# new paragraph
/^\.PP$/ {
	$0 = "\n\n"
}

# new paragraph
/^\.P$/ {
	$0 = "\n\n"
}

# new indented paragraph (ignored)
/^\.IP$/ {
	$0 = "\n\n"
}

# new titled paragraph (converted to definition list item)
/^\.TP ?/ {
	$0 = "\n\n* "
	tp = 2
}

# begin indented section (interpreted as alias for .EX)
/^\.RS$/ {
	example = 1
	$0 = "\n```"

}

# end indented section (interpreted as alias for .EE)
/^\.RE$/ {
	example = 0
	$0 = "```"
}

# begin example (converted as ```)
/^\.EX$/ {
	example = 1
	$0 = "\n```"

}

# end example (converted as ```)
/^\.EE$/ {
	example = 0
	$0 = "```"
}

# line break
/^\.br$/ {
	$0 = "\n"
}

# bold font
/^\.B / {
	removetag()
	$0 = sprintf("**%s** ", $0)
}

# italic font
/^\.I / {
	removetag()
	$0 = sprintf("*%s* ", $0)
}

# roman-to-bold font
/^\.RB / {
	twotags("", "**")
}

# bold-to-roman font
/^\.BR / {
	twotags("**", "")
}

# bold-to-italic font
/^\.BI / {
	twotags("**", "_")
}

# italic-to-bold font
/^\.IB / {
	twotags("_", "**")
}

# italic-to-roman font
/^\.IR / {
	twotags("_", "")
}

# roman-to-italic font
/^\.RI / {
	twotags("", "_")
}

# process and print line
{
	inline()
	if (!example) {
		# this is necessary for <...> don't be interpreted as an html tag
		gsub("<", "\\<")
	}
	printf "%s", $0
	if (example)
		print ""
	if (pd == 0) {
		if (tp == 1)
			printf "\n\n  "
		if (tp)
			tp--
	}
	if (pd)
		pd = 0
}
