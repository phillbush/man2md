#!/usr/bin/awk -f
# man2md generate a markdown document from a man page
# man2md is in public domain

BEGIN {
	example = 0
	emphasis = ""
}

function removetag() {
	sub("^\...? *", "")
}

function twotags(m1, m2, n, beg, i) {
	removetag()
	beg = 1
	if (match($0, "\"")) {
		n = split($0, a, "\"")
		if (n > 0 && a[n] == "")
			n--
		if (n > 1 && a[1] == "")
			beg = 2
	} else {
		n = split($0, a)
	}
	$0 = ""
	for (i = beg; i <= n; i++) {
		if ((i - beg + 1) % 2) {
			sub("^ *", "&" m1, a[i])
			sub(" *$", m1 "&", a[i])
		} else {
			sub("^ *", "&" m2, a[i])
			sub(" *$", m2 "&", a[i])
		}
		$0 = $0 a[i]
	}
	$0 = $0 " "
}

function inline(n, c) {
	gsub("\\\\\\*?\\(dq", "\"")
	gsub("\\\\\\*?\\(lq", "â€œ")
	gsub("\\\\\\*?\\(rq", "â€")
	for (i in ds)
		gsub("\\\\\\*\\(" i, ds[i])
	gsub("\\\\-", "-")
	gsub("\\\\e", "\\")
	gsub("\\\\s([+-]?[0-9]|\\([+-]?[0-9][0-9]|[+-]?\\([0-9][0-9])", "")
	while ((n = match($0, "\\\\f.")) != 0) {
		c = substr($0, n + 2, 1)
		if (c == "B") {
			emphasis = "**"
		} else if (c == "I") {
			emphasis = "_"
		}
		sub("\\\\f.", emphasis)
	}
}

/^\.(if|ie|el) / {
	if (match($0, "\\\\\\{") != 0) {
		while (match($0, "^\\.\\\\\\}") == 0) {
			getline
		}
	}
	$0 = ""
}

/^[^.]/ {
	if (!example)
		$0 = $0 " "
}

/^\.ds / {
	removetag()
	n = split($0, a)
	sub("^[^ ]* ", "")
	ds[a[1]] = $0
	$0 = ""
}

/^\.\\"/ {
	$0 = ""
}

/^\.PD/ {
	$0 = ""
	pd = 1
}

/^\.TH / {
	split($0, a)
	$0 = sprintf("# %s(%s)\n", a[2], a[3])
}

/^\.SH / {
	removetag()
	$0 = sprintf("\n\n## %s\n\n", $0)
}

/^\.SS / {
	removetag()
	$0 = sprintf("\n\n### %s\n\n", $0)
}

/^\.PP$/ {
	$0 = "\n\n"
}

/^\.PP$/ {
	$0 = "\n\n"
}

/^\.P$/ {
	$0 = "\n\n"
}

/^\.IP$/ {
	$0 = "\n\n"
}

/^\.TP$/ {
	$0 = "\n\n* "
	tp = 2
}

/^\.RS$/ {
	example = 1
	$0 = "\n```"

}

/^\.RE$/ {
	example = 0
	$0 = "```"
}

/^\.EX$/ {
	example = 1
	$0 = "\n```"

}

/^\.EE$/ {
	example = 0
	$0 = "```"
}

/^\.br$/ {
	$0 = "\n"
}

/^\.B / {
	removetag()
	$0 = sprintf("**%s** ", $0)
}

/^\.I / {
	removetag()
	$0 = sprintf("*%s* ", $0)
}

/^\.RB / {
	twotags("", "**")
}

/^\.BR / {
	twotags("**", "")
}

/^\.BI / {
	twotags("**", "_")
}

/^\.IB / {
	twotags("_", "**")
}

/^\.IR / {
	twotags("_", "")
}

/^\.RI / {
	twotags("", "_")
}

{
	inline()
	if (!example) {
		gsub("<", "\\<")
	}
	printf "%s", $0
	if (example)
		print ""
	if (pd == 0) {
		if (tp == 1)
			printf "\n\n  "
		if (tp)
			tp--
	}
	if (pd)
		pd = 0
}
